buildscript {
    ext {
        kotlin_version = '1.1.2-3'
        junit5_plugin_version = '1.0.0-M4'
        spek_version = '1.1.1'
        jmh_version = '1.19'
        jackson_version = '2.8.7'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit5_plugin_version"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
    }

}

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin:  "me.champeau.gradle.jmh"

group = 'de.gtrefs'
version = '0.0.1-SNAPSHOT'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

sourceSets {
    examples {
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }

    benchmark {
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

["examples", "benchmark"].findAll {project.hasProperty(it)}.each {
    def sourceSet = sourceSets.maybeCreate(it)
    sourceSets.test.kotlin.srcDirs += sourceSet.kotlin.srcDirs
    sourceSets.test.resources.srcDirs += sourceSet.resources.srcDirs
}

jmh {
    iterations = 10
    warmupIterations = 10
    fork = 2
    threads = 1
    profilers = ["stack"]
}

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    maven { url "https://kotlin.bintray.com/kotlinx/" }
}

dependencies {
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.14'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.14'

    testCompile 'com.google.guava:guava:21.0'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "com.winterbe:expekt:0.5.0"
    testCompile "org.jetbrains.spek:spek-api:$spek_version"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"

    jmh group: 'org.openjdk.jmh', name: 'jmh-core', version: "$jmh_version"
    jmh group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: "$jmh_version"

}
